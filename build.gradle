buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "Forge-Repo"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'SpongePowered-Repo'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}


apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'


sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

version = "B1"
group = "me.kodingking"
archivesBaseName = "Kodax"

minecraft {
    version = "1.8.9"
    tweakClass = "me.kodingking.kodax.injection.LaunchInjector"
    mappings = "stable_20"
    runDir = 'run'
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'SpongePowered-Repo'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url "https://jitpack.io" }
    maven { url "https://raw.github.com/KodaxClient/Maven/repository/" }
}

dependencies {
    embed('org.spongepowered:mixin:0.7.7-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    embed('me.kodingking:KodaxNetty:1.2-SNAPSHOT') {
        exclude module: 'netty-all'
        exclude module: 'gson'
    }

    embed 'com.jagrosh:DiscordIPC:0.4'
    embed 'org.slick2d:slick2d-core:1.0.2'
    embed 'com.github.fcannizzaro:material-colors:0.1.0'
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest.attributes(
            "MixinConfigs": 'mixins.kodax.json',
            "TweakClass": 'me.kodingking.kodax.injection.LaunchInjector',
            "TweakOrder": 0,
            "Manifest-Version": 1.0
    )
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixins.kodax.refmap.json"
}